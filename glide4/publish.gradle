group = rootProject.group
version = rootProject.ext.libraryVersion

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/kirich1409/SvgGlidePlugins'
def gitRepoUrl = 'https://github.com/kirich1409/SvgGlidePlugins.git'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('apiKey') ? project.property('apiKey') : System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = 'svg-glide-plugin'
        desc = 'SVG support for Glide 4'
        websiteUrl = siteUrl
        issueTrackerUrl = "$siteUrl/issues"
        vcsUrl = gitRepoUrl
        licenses = ['Apache-2.0']
        labels = ['android', 'glide', 'svg', 'glidev4']
//        publish true
        publicDownloadNumbers = true

        githubRepo = 'kirich1409/SvgGlidePlugins' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file

        version {
            name = rootProject.ext.libraryVersion
            desc = 'SVG support for Glide 4'
            released = new Date()
            gpg {
                sign = true
                passphrase = project.hasProperty('gpgPassword') ? project.property('gpgPassword') : System.getenv('BINTRAY_GPG_PASSWORD')
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'SVG support for Glide 4'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'kirich1409'
                        name 'Kirill Rozov'
                        email 'krl.rozov@gmail.com'
                    }
                }
                scm {
                    connection gitRepoUrl
                    developerConnection gitRepoUrl
                    url siteUrl
                }
            }
        }
    }
}
